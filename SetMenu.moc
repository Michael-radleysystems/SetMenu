dofile("etal\\lib\\keys.moc");	// load the function key lib

local nSetMenuStDtDpt = 12;
local nSetMenuMainDpt = 13;
local nSetCourseDepartment = 14;
local nSDForSale = 0;
local nMainForSale = 0;
local nFontSize = 16;
local szFont = "Arial";
local nWindowID = 0;
local tbDimensions = {};
local bDisplayWindow = false;
local bDebugPrinting = false;
local bEnabled = true;
local nTimer = 0;

function OnInit()
{
	// enable debug printing (or not)
    ICR_EnableDebugLog(bDebugPrinting);
    ICR_SetLocalUserData("bDebugPrinting",bDebugPrinting);
    
    //debug console 
    if(bDebugPrinting)
    {
        // only set these if debug printing is on, we don't want to set console to false as there's only one and it'll stop any script loaded earlier from using it.
        ICR_SetFeature("CONSOLE",bDebugPrinting);
        ICR_SetFeature("AUTORESTART",bDebugPrinting);
    }
	
	//Create a window ID from our window xml
	nWindowID = ICR_SalesModeWindow_Add("window.xml");

    //Enable a copy of the window we are going to use the window later
    //to allow the user to select which clerks they want notes for
    ICR_Window_Enable(nWindowID, false); 

    //get dimensions of the window we just created
    tbDimensions = ICR_GetWindowDimensions(nWindowID);
	
	
	//set all the setting in to the local settings 
	ICR_SetLocalUserData("bEnabled",bEnabled,true)
	ICR_SetLocalUserData("nSetMenuStDtDpt", nSetMenuStDtDpt, true);
	ICR_SetLocalUserData("nSetMenuMainDpt", nSetMenuMainDpt, true);
	ICR_SetLocalUserData("nSetCourseDepartment", nSetCourseDepartment, true);
	
	//do a mode change to load setting.
	AfterModeChange(10,7);
}

function AfterModeChange(nFrom,nTo)
{
	if(nFrom != 10)
	{
		return;
	}
	
	ICR_SetLocalUserData("bDebugPrinting",bDebugPrinting);
	
	bEnabled = ICR_GetLocalUserData("bEnabled");
	nSetMenuStDtDpt = ICR_GetLocalUserData("nSetMenuStDtDpt");
	nSetMenuMainDpt = ICR_GetLocalUserData("nSetMenuMainDpt");
	nSetCourseDepartment = ICR_GetLocalUserData("nSetCourseDepartment");
}

function BeforeKeyPress(nFile, nRecord, nKeyType)
{
	if(!bEnabled)
	{
		return true;
	}
	
	//Check if the keytype is a finalize or other charge type
	if((nKeyType > 0 && nKeyType < 8) || nKeyType == 11 || nKeyType == 13 || ((nKeyType == 110 && nRecord > 186)&&(nKeyType == 110 && nRecord < 191)))
	{
		//Call a count 
		AvaiableCourseCount();
		//check if any courses are negitive
		if(nSDForSale < 0 || nMainForSale < 0)
		{
			//Inform the user they have to charge for a course
			ICR_MessageBox("Not all Set Menu Items are charged for.","","",0);
			return false;
		}
		//Check if we have any unused course and are we selling 
		if((nSDForSale > 0 || nMainForSale > 0) && (nKeyType != 7))
		{
			//Ask the user do they wish to complete the sale
			if(ICR_MessageBox("You have unused courses","Continue","",1))
			{
				return true;
			}
			return false;
		}		
	}
	
	//Check for other buttons
	if(nFile != 1) 
	{
		return true;
	}
	
	//Grab the PLU 
	local tThisPLU = ICR_GetPGMData(1,nRecord);
	
	//Is it one of our set menu	starters or desserts
	if(tThisPLU.DEPT == nSetMenuStDtDpt)
	{	
		if(bDebugPrinting) print("Department of plu pressed: "+tThisPLU.DEPT);
		
		//Call a Count
		AvaiableCourseCount();		
		
		//Do we have enough courses left for a sale
		if(nSDForSale <= 0)
		{
			// Display a message saying you did not charge for this course
			ICR_MessageBox("You Did not charge for this course","","",0);
			//Stop the button press
			return false;
		}		
	}
	
	//Is it one of our set menu	Mains
	if(tThisPLU.DEPT == nSetMenuMainDpt)
	{	
		if(bDebugPrinting) print("Department of plu pressed: "+tThisPLU.DEPT);
		
		//Call a Count
		AvaiableCourseCount();		
		
		//Do we have enough courses left for a sale
		if(nMainForSale <= 0)
		{
			// Display a message saying you did not charge for this course
			ICR_MessageBox("You Did not charge for this course","","",0);
			//Stop the button press
			return false;
		}		
	}	
	return true;		
}

function OnWindowPaint(nID)
{
    //another function call from ICRTouch askng what needs to happen to make our window redraw
	if(nID == nWindowID)
    {
        Paint_Window();
    }	
}

function Paint_Window()
{
	//Creae a Window to be painted to the screem
	local nTitleFont = ICR_CreateFont(szFont, nFontSize);
	ICR_SelectFont(nTitleFont);
	ICR_SetText_VAlign(0);
	ICR_SetText_HAlign(0);
	ICR_SetText_Colour(0xFFFFFF);	
	ICR_DrawText(0, 0, "S/D: "+nSDForSale+" M: "+nMainForSale);
}

function AvaiableCourseCount()
{
	//Call the cleark buffer
	local tClerkBuffer = ICR_GetClerkBuffer_Index();
	
	//Set the temp counts to zero
	local nTempMain = 0;
	local nTempSD = 0;
	local nCountFlag = 0;
	
	//loop through the buffer
	for(local nCount = 1 ; nCount <= tClerkBuffer.REGPOS ; nCount++)
    {
		//Call in the next line in the buffer
        local tNextInLine = ICR_GetClerkBuffer_Item(nCount);
		//Continue to the next itteration of the loop if it is not a plu
        if(tNextInLine.FILE != 1)continue;
		local tThisPLU = ICR_GetPGMData(1,tNextInLine.RECORD);
		
		//Is this one of our course counters
		if(tThisPLU.DEPT == nSetCourseDepartment)
		{
			if(bDebugPrinting) print("Price level 4: "+tThisPLU.PRICE4L1);
			
			//Check if the number of courses is grater than 1
			if(tThisPLU.PRICE4L1 > 1)
			{
				nTempMain++;
				nTempSD = nTempSD + tThisPLU.PRICE4L1 -1;
			}
			else
			{
				nTempSD++;
			}
			
			// Set the window to display
			bDisplayWindow = true;
			nCountFlag++;
		}
		
		//Remove a starter or dessert course
		if(tThisPLU.DEPT == nSetMenuStDtDpt)
		{
			nTempSD--;
			bDisplayWindow = true;
			nCountFlag++;
		}
		
		//Remove a main course
		if(tThisPLU.DEPT == nSetMenuMainDpt)
		{
			nTempMain--;
			bDisplayWindow = true;
			nCountFlag++;
		}
	}
	
	//Was there any of our set menu items in the sale
	if(nCountFlag == 0)
	{
		//Set the window not to show
		bDisplayWindow = false;
	}
	
	if(bDebugPrinting) print ("Count after looping through reg window: S/D: "+nTempSD+" M: "+nTempMain);
	
	//Set the counted courses to for sale courses
	nSDForSale = nTempSD;
	nMainForSale = nTempMain;
	
	if(bDebugPrinting) print ("Updated Courses for sale S/D: "+nSDForSale+" M: "+nMainForSale);
}

function AfterKeyPress(nFile, nRecord, nKeyType)
{	
	if(!bEnabled)
	{
		return true;
	}
	
	//After an error correct count and redraw
	if(nKeyType == 37)
	{
		AvaiableCourseCount();
	}
	
	//After floor plan count and redraw
	if(nKeyType == 62)
	{
		AvaiableCourseCount();
	}
	
	//After a finalize key or charge stop displaying the window and redraw
	if((nKeyType > 0 && nKeyType < 8) || nKeyType == 11 || nKeyType == 13 || ((nKeyType == 110 && nRecord > 186)&&(nKeyType == 110 && nRecord < 191)))
	{
		bDisplayWindow = false
	}
	
	RedrawWindow();
	
	//After a button press check if it is a plu
	if(nFile != 1)
	{
		return true; //not a PLU
	}
	
	//Grab the PLU 
	local tThisPLU = ICR_GetPGMData(1,nRecord);
	//Is it one of our set menu count and redraw
	if(tThisPLU.DEPT == nSetMenuStDtDpt || tThisPLU.DEPT == nSetCourseDepartment || tThisPLU.DEPT == nSetMenuMainDpt)
	{
		AvaiableCourseCount();		
	}
	//Redraw after every plu
	RedrawWindow();
}

function AfterRegItemSelect(nItem)
{
	RedrawWindow();
}

//function for redrawing our window
function RedrawWindow()
{
	ICR_Window_Enable(nWindowID, bDisplayWindow);
	ICR_RedrawWindow(21, nWindowID);
}

//Timer called to redraw my window
function OnTimer(nTimerID,nSysTime)
{
	if(nTimerID == nTimer)
	{
		ICR_StopTimer(nTimerID);
		RedrawWindow();
	}
}

function OnEnterPGMMenu(nSub)
{
	local nID=0;
	ICR_PurgeProgramSettings();
	switch(nSub)
	{
		case 0:
			ICR_AdvertiseProgramSetting("Set Menu is enabled","bEnabled");
			nID = ICR_AdvertiseProgramSetting("Starter and Desert Department","nSetMenuStDtDpt");			
			ICR_ProgramItem_SetOptionList(nID,1);
			nID = ICR_AdvertiseProgramSetting("Mains Department","nSetMenuMainDpt");			
			ICR_ProgramItem_SetOptionList(nID,2);
			nID = ICR_AdvertiseProgramSetting("Set Menu Course Department","nSetCourseDepartment");			
			ICR_ProgramItem_SetOptionList(nID,3);
			break;
		default:
			ICR_AdvertiseProgramHeading("This menu doesn't exist");
			break;
	}
}

function OnEnterPGMOptionList(nID)
{
	ICR_PurgeProgramOptionList();
	
	local nMax = ICR_GetFileSize(55);
	
	switch(nID)	
	{
		case 1:
			ICR_ProgramOptionList_Add("NOT USED",0);
			for (local nCount = 1; nCount <= nMax; nCount++)
            {
                local tThisPGM = ICR_GetPGMData(55,nCount);
                ICR_ProgramOptionList_Add(tThisPGM.NAME,nCount);
            }
			break;
		case 2:
			ICR_ProgramOptionList_Add("NOT USED",0);
			for (local nCount = 1; nCount <= nMax; nCount++)
            {
                local tThisPGM = ICR_GetPGMData(55,nCount);
                ICR_ProgramOptionList_Add(tThisPGM.NAME,nCount);
            }
			break;
		case 3:
			ICR_ProgramOptionList_Add("NOT USED",0);
			for (local nCount = 1; nCount <= nMax; nCount++)
            {
                local tThisPGM = ICR_GetPGMData(55,nCount);
                ICR_ProgramOptionList_Add(tThisPGM.NAME,nCount);
            }
			break;
		default:
			break;
	}
}

/**********************************************************************
OnBarcode(szBarcodeData)
Occurs when Barcode scanned but before pos executes bar code.
Return false to stop barcode. true to continue.
**********************************************************************/

function OnBarcode(szBarcodeData)
{
    // Was scanned thing a plu in our program ?
    local nScannedPLU = ICR_GetPLURecNumber(szBarcodeData);
    if((nScannedPLU == null) || (nScannedPLU == 0 ))return true; // Nope. Let till deal with it.
    // If its a plu. do the before press checking we do on the Key.
    if(bDebugPrinting)print("Invoking BKP for BarcodePlu "+nScannedPLU);
    return BeforeKeyPress(1, nScannedPLU, 1000);
}
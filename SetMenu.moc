dofile("etal\\lib\\keys.moc");	// load the function key lib

local nSetMenuStDtDpt = 12;
local nSetMenuMainDpt = 13;
local nSetCourseDepartment = 14;
local nSDForSale = 0;
local nMainForSale = 0;
local nFontSize = 16;
local szFont = "Arial";
local nWindowID = 0;
local tbDimensions = {};
local bDisplayWindow = false;
local bDebugPrinting = false;

function OnInit()
{
	// enable debug printing (or not)
    ICR_EnableDebugLog(bDebugPrinting);
    ICR_SetLocalUserData("bDebugPrinting",bDebugPrinting);
    
    //debug console 
    if(bDebugPrinting)
    {
        // only set these if debug printing is on, we don't want to set console to false as there's only one and it'll stop any script loaded earlier from using it.
        ICR_SetFeature("CONSOLE",bDebugPrinting);
        ICR_SetFeature("AUTORESTART",bDebugPrinting);
    }
	
	//Create a window ID from our window xml
	nWindowID = ICR_SalesModeWindow_Add("window.xml");

    //Enable a copy of the window we are going to use the window later
    //to allow the user to select which clerks they want notes for
    ICR_Window_Enable(nWindowID, false); 

    //get dimensions of the window we just created
    tbDimensions = ICR_GetWindowDimensions(nWindowID);
}

function BeforeKeyPress(nFile, nRecord, nKeyType)
{
	if(nKeyType > 0 && nKeyType < 8)
	{
		AvaiableCourseCount();
		if(nSDForSale < 0 || nMainForSale < 0)
		{
			ICR_MessageBox("Not all Set Menu Items are charged for.","","",0);
			return false;
		}
		
		if((nSDForSale > 0 || nMainForSale > 0) && (nKeyType != 7))
		{
			if(ICR_MessageBox("You have unused courses","Continue","",1))
			{
				return true;
			}
			return false;
		}		
	}
	
	if(nFile != 1) // Not a PLU so move on
	{
		return true;
	}
	
	//Grab the PLU 
	local tThisPLU = ICR_GetPGMData(1,nRecord);
	
	//Is it one of our set menu	
	if(tThisPLU.DEPT == nSetMenuStDtDpt)
	{	
		if(bDebugPrinting) print("Department of plu pressed: "+tThisPLU.DEPT);
		
		AvaiableCourseCount();		
		
		if(nSDForSale <= 0)
		{
			// Display a message saying you did not charge for this course
			ICR_MessageBox("You Did not charge for this course","","",0);
			//Stop the button press
			return false;
		}		
	}
	
	if(tThisPLU.DEPT == nSetMenuMainDpt)
	{	
		if(bDebugPrinting) print("Department of plu pressed: "+tThisPLU.DEPT);
		
		AvaiableCourseCount();		
		
		if(nMainForSale <= 0)
		{
			// Display a message saying you did not charge for this course
			ICR_MessageBox("You Did not charge for this course","","",0);
			//Stop the button press
			return false;
		}		
	}	
	return true;		
}

function OnWindowPaint(nID)
{
    //another function call from ICRTouch askng what needs to happen to make our window redraw
	if(nID == nWindowID)
    {
        Paint_Window();
    }
}

function Paint_Window()
{
	local nTitleFont = ICR_CreateFont(szFont, nFontSize);
	ICR_SelectFont(nTitleFont);
	ICR_SetText_VAlign(0);
	ICR_SetText_HAlign(0);
	ICR_SetText_Colour(0xFFFFFF);	
	ICR_DrawText(0, 0, "S/D: "+nSDForSale+" M: "+nMainForSale);
}

function AvaiableCourseCount()
{
	local tClerkBuffer = ICR_GetClerkBuffer_Index();
	
	local nTempMain = 0;
	local nTempSD = 0;
	
	for(local nCount = 1 ; nCount <= tClerkBuffer.REGPOS ; nCount++)
    {
        local tNextInLine = ICR_GetClerkBuffer_Item(nCount);
        if(tNextInLine.FILE != 1)continue;
		local tThisPLU = ICR_GetPGMData(1,tNextInLine.RECORD);
		
		if(tThisPLU.DEPT == nSetCourseDepartment)
		{
			if(bDebugPrinting) print("Price level 4: "+tThisPLU.PRICE4L1);
			
			if(tThisPLU.PRICE4L1 > 1)
			{
				nTempMain++;
				nTempSD = nTempSD + tThisPLU.PRICE4L1 -1;
			}
			else
			{
				nTempSD++;
			}
			
			bDisplayWindow = true;
		}
		
		if(tThisPLU.DEPT == nSetMenuStDtDpt)
		{
			nTempSD--;
			bDisplayWindow = true;
		}
		
		if(tThisPLU.DEPT == nSetMenuMainDpt)
		{
			nTempMain--;
			bDisplayWindow = true;
		}
	}
	
	if(bDebugPrinting) print ("Count after looping through reg window: S/D: "+nTempSD+" M: "+nTempMain);	
	nSDForSale = nTempSD;
	nMainForSale = nTempMain;
	if(bDebugPrinting) print ("Updated Courses for sale S/D: "+nSDForSale+" M: "+nMainForSale);
}

function AfterKeyPress(nFile, nRecord, nKeyType)
{	
	if(nKeyType == 37)
	{
		AvaiableCourseCount();
		RedrawWindow();
	}
	
	if(nKeyType == 62)
	{
		AvaiableCourseCount();
		RedrawWindow();
	}
	
	if(nKeyType > 0 && nKeyType < 8)
	{
		bDisplayWindow = false
		RedrawWindow();
	}
	
	if(nFile != 1)
	{
		return true; //not a PLU
	}
	
	//Grab the PLU 
	local tThisPLU = ICR_GetPGMData(1,nRecord);
	//Is it one of our set menu
	if(tThisPLU.DEPT == nSetMenuStDtDpt || tThisPLU.DEPT == nSetCourseDepartment || tThisPLU.DEPT == nSetMenuMainDpt)
	{
		AvaiableCourseCount();
		RedrawWindow();
	}
}

function RedrawWindow()
{
	ICR_Window_Enable(nWindowID, bDisplayWindow);
	ICR_RedrawWindow(21, nWindowID);
}